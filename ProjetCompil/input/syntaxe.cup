// ---------------------------------------------------------------------------
// syntaxe.cup
// Fichier cup qui définit l'analyseur syntaxique
// ---------------------------------------------------------------------------

package fr.esisar.compilation.syntaxe;

import java_cup.runtime.*;
import fr.esisar.compilation.global.src.*;
import java.io.InputStream;

/* Code pour l'analyseur syntaxique. */
parser code {:

   /**
    * Réalise l'analyse syntaxique du fichier passé en argument (ou de 
    * l'entrée standard si args est un tableau vide) et retourne 
    * l'abstrait correspondant.
    * Lève l'exception ErreurLexicale en cas d'erreur lexicale, et 
    * ErreurSyntaxe en cas d'erreur de syntaxe.
    */
   public static Arbre analyseSyntaxique(String[] args) 
         throws Exception, ErreurLexicale, ErreurSyntaxe {

      // On récupère le fichier à analyser
      InputStream fichierCas = ArgsFichier.ouvrir(args);

      // Création de l'analyseur lexical
      Lexical analyseurLex = new Lexical(fichierCas);

      // Création de l'analyseur syntaxique
      parser analyseurSynt = new parser(analyseurLex);

      // Appel de l'analyseur syntaxique et récupération de l'arbre résultat
      Object result = analyseurSynt.parse().value;
      Arbre arbre = (Arbre) result;
      
      // On retourne l'arbre abstrait construit
      return arbre;
   }

   /**
    * Affiche un message d'erreur en cas d'erreur de syntaxe.
    */
   public void syntax_error(Symbol cur_token) {
      // Affichage du message d'erreur, avec le numéro de ligne 
      System.out.println("Erreur de syntaxe ... ligne " + numLigne()) ;
   }

   /**
    * Méthode appelée en cas d'erreur de syntaxe.
    */
   public void unrecovered_syntax_error(Symbol cur_token)
         throws ErreurSyntaxe {
      throw new ErreurSyntaxe();
   }

   /**
    * Retourne le numéro de ligne courant.
    */
   public int numLigne() {
      // On récupère l'analyseur lexical
      Lexical anaLex = (Lexical) getScanner();
      return anaLex.numLigne();
   }
:};

/* Terminaux (tokens retournés par l'analyseur lexical. */
terminal String IDF;          // Identificateur
terminal Integer CONST_ENT;   // Constante entière
terminal Float CONST_REEL;    // Constante réelle
terminal String CONST_CHAINE; // Constante chaîne

/* Mots réservés */
terminal   AND,         ARRAY,       BEGIN;
terminal   DIV,         DO,          DOWNTO;
terminal   ELSE,        END,         FOR;
terminal   IF,          MOD,         NEW_LINE;
terminal   NOT,         NULL,        OF;
terminal   OR,          PROGRAM,     READ;
terminal   THEN,        TO,          WHILE;
terminal   WRITE;

/* Opérateurs */
terminal INF, SUP, EGAL, DIFF, // "<", ">", "=", "/="
         INF_EGAL, SUP_EGAL,   // "<=", ">="
         PLUS, MOINS,          // "+", "-"
         MULT, DIV_REEL;       // "*", "/"

/* Autres terminaux */
terminal PAR_OUVR, PAR_FERM; // "(", ")" 
terminal DOUBLE_POINT;       // ".."
terminal DEUX_POINTS;        // ":"
terminal VIRGULE;            // ","
terminal POINT_VIRGULE;      // ";"
terminal CROCH_OUVR;         // "["
terminal CROCH_FERM;         // "]"
terminal AFFECT;             // ":="
terminal POINT;              // "."

/* Définition des non terminaux de la grammaire */
non terminal Arbre program;
non terminal Arbre liste_decl;
non terminal Arbre liste_inst;
non terminal Arbre inst;
non terminal Arbre decl;
non terminal Arbre liste_idf;
non terminal Arbre type;
non terminal Arbre idf;
non terminal Arbre type_intervalle;
non terminal Arbre constante;
non terminal Arbre const;
non terminal Arbre pas;
non terminal Arbre liste_exp;
non terminal Arbre exp;
non terminal Arbre facteur;
non terminal Arbre place;

/*
   On définit les priorités des opérateurs
   Les priorités vont dans l'ordre croissant
   On déclare egalement l'associativité des opérateurs
   (left, right, ou nonassoc)
*/

precedence nonassoc INF, SUP, EGAL, DIFF, INF_EGAL, SUP_EGAL;
precedence left PLUS, MOINS, OR;
precedence left MULT, DIV_REEL, DIV, MOD, AND;
precedence nonassoc NOT;

program ::= PROGRAM:p liste_decl:a1 BEGIN liste_inst:a2 END POINT
   {:
      RESULT = Arbre.creation2(Noeud.Programme, a1, a2, pleft);
      // pleft : Numéro de ligne du mot réservé "program".
   :}
   ;
liste_decl ::= liste_decl:a1 decl:a2 POINT_VIRGULE
   {:
      RESULT = Arbre.creation2(Noeud.ListeDecl, a1, a2, a2.getNumLigne());
   :}
   |  // epsilon
   {:
      RESULT = Arbre.creation0(Noeud.Vide, parser.numLigne());
   :}
   ;

// ---------------------------------------------------------------------------
// Debut des regles definies par le groupe Sauger-Jarry-Thomas-Bellanger (2018)
// ---------------------------------------------------------------------------

decl ::= liste_idf:a DEUX_POINTS type:b
   {:
      RESULT = Arbre.creation2(Noeud.Decl,a,b,b.getNumLigne());
   :}
   ;
liste_idf ::= liste_idf:a VIRGULE idf:b
   {: 
      RESULT = Arbre.creation2(Noeud.ListeIdent,a,b,b.getNumLigne());
   :}
   |  idf:b
   {:
      vide = Arbre.creation0(Noeud.Vide,parser.numLigne());
      RESULT = Arbre.creation2(Noeud.ListeIdent,vide,b,b.getNumLigne());
   :}
   ;

type ::= idf:a
   {:
      RESULT = a;
   :}
   |  type_intervalle:a
   {:
      RESULT = a;
   :}
   |  ARRAY CROCH_OUVR type_intervalle:a CROCH_FERM OF type:b
   {:
      RESULT = Arbre.creation2(Noeud.Tableau,a,b,a.getNumLigne());
   :}
   ;

type_intervalle ::= constante:a DOUBLE_POINT constante:b
   {:
      RESULT = Arbre.creation2(Noeud.Intervalle,a,b,a.getNumLigne());
   :}
   ;

constante ::= PLUS const:a
   {:
      RESULT = Arbre.creation1(Noeud.PlusUnaire(a,aleft));
   :}
   |  MOINS const:a
   {: 
      RESULT = Arbre.creation1(Noeud.MoinsUnaire(a,aleft));
   :}
   |  const:a
   {:
      RESULT = a;
   :}
   ;

const ::= CONST_ENT:a
   {:
      RESULT = Arbre.creationEntier(a,aleft);
   :}
   |  idf:a
   {:
      RESULT = a;
   :}
   ;
idf ::= IDF
   {:
      RESULT = Arbre.creationIdent(a,aleft);
   :}
   ;

liste_inst ::= liste_inst:a inst:b POINT_VIRGULE
   {: 
      RESULT = Arbre.creation2(Noeud.ListeInst,a,b,bleft);
   :}
   |  inst:b POINT_VIRGULE
   {:
      vide = Arbre.creation0(Noeud.Vide,parser.numLigne());
      RESULT = Arbre.creation2(Noeud.ListeInst,vide,b,bleft);
   :}
   ;

inst ::= NULL
   {:
      RESULT = Arbre.creation0(Noeud.Nop,parser.numLigne());
   :}
   |  place:a AFFECT exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Affect,a,b,aleft);
   :}
   |  FOR pas:a DO liste_inst:b END
   {:
      RESULT = Arbre.creation2(Noeud.Pour,a,b,aleft);
   :}
   |  WHILE exp:a DO liste_inst:b END
   {:
      RESULT = Arbre.creation2(Noeud.TantQue,a,b,aleft);
   :}
   |  IF exp:a THEN liste_inst:b END
   {:
      vide = Arbre.creation0(Noeud.Vide,parser.numLigne());
      RESULT = Arbre.creation3(Noeud.Si,a,b,vide,aleft);
   :}
   |  IF exp:a THEN liste_inst:b ELSE liste_inst:c END
   {:
      RESULT = Arbre.creation3(Noeud.Si,a,b,c,aleft);
   :}
   |  WRITE PAR_OUVR liste_exp:a PAR_FERM
   {:
      RESULT = Arbre.creation1(Noeud.Ecriture,a,aleft);
   :}
   |  READ PAR_OUVR place:a PAR_FERM
   {:
      RESULT = Arbre.creation1(Noeud.Lecture,a,aleft);
   :}
   |  NEW_LINE
   {:
      RESULT = Arbre.creation0(Noeud.Ligne,parser.numLigne());
   :}
   ;

pas ::= idf:a AFFECT exp:b TO exp:c
   {:
      RESULT = Arbre.creation3(Noeud.Increment,a,b,c,aleft);
   :}
   |  idf:a AFFECT exp:b DOWNTO exp:c
   {: 
      RESULT = Arbre.creation3(Noeud.Decrement,a,b,c,aleft);
   :}
   ;

liste_exp ::= liste_exp:a VIRGULE exp:b
   {:
      RESULT = Arbre.creation2(Noeud.ListeExp,a,b,b.getNumLigne());
   :}
   |  exp:b
   {:
      vide = Arbre.creation0(Noeud.Vide,parser.numLigne());
      RESULT = Arbre.creation2(Noeud.ListeExp,vide,b,b.getNumLigne());
   :}
   ;
   
exp ::= facteur:a
   {:
      RESULT = a;
   :}
   |  exp:a AND exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Et,a,b,b.getNumLigne());
   :}
   |  exp:a OR exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Ou,a,b,b.getNumLigne());
   :}
   |  exp:a EGAL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Egal,a,b,b.getNumLigne());
   :}
   |  exp:a INF_EGAL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.InfEgal,a,b,b.getNumLigne());
   :}
   |  exp:a SUP_EGAL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.SupEgal,a,b,b.getNumLigne());
   :}
   |  exp:a DIFF exp:b
   {:
      RESULT = Arbre.creation2(Noeud.NonEgal,a,b,b.getNumLigne());
   :}
   |  exp:a INF exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Inf,a,b,b.getNumLigne());
   :}
   |  exp:a SUP exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Sup,a,b,b.getNumLigne());
   :}
   |  exp:a PLUS exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Plus,a,b,b.getNumLigne());
   :}
   |  exp:a MOINS exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Moins,a,b,b.getNumLigne());
   :}
   |  exp:a MULT exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Mult,a,b,b.getNumLigne());
   :}
   |  exp:a DIV_REEL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.DivReel,a,b,b.getNumLigne());
   :}
   |  exp:a MOD exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Reste,a,b,b.getNumLigne());
   :}
   |  exp:a DIV exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Quotient,a,b,b.getNumLigne());
   :}
   |  PLUS facteur:a
   {: 
      RESULT = Arbre.creation1(Noeud.PlusUnaire,a,a.getNumLigne());
   :}
   |  MOINS facteur:a
   {: 
      RESULT = Arbre.creation1(Noeud.MoinsUnaire,a,a.getNumLigne());
   :}
   |  NOT facteur:a
   {: 
      RESULT = Arbre.creation1(Noeud.Non,a,a.getNumLigne());
   :}
   ;

facteur ::= CONST_ENT:a
   {:
      RESULT = Arbre.creationEntier(a,aleft);
   :}
   |  CONST_REEL:a
   {: 
      RESULT = Arbre.creationReel(a,aleft);
   :}
   |  CONST_CHAINE:a
   {:
      RESULT = Arbre.creationChaine(a,aleft);
   :}
   |  place:a
   {:
      RESULT = a;
   :}
   |  PAR_OUVR exp:a PAR_FERM
   {:
      RESULT = a;
   :}
   ;

place ::= idf:a
   {:
      RESULT = a;
   :}
   |  place:a CROCH_OUVR exp:b CROCH_FERM
   {:
      RESULT = Arbre.creation2(Noeud.Index,a,b,b.getNumLigne());
   :}
   ;
